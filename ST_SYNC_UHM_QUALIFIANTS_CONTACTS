AS 
/* 
****************** DOCUMENTATION TEMPLATE ******************** 
-- DID AUTHOR: ERICK VAN HOOREN 
-- DID CREATIONDATE: 2022-09-09 
-- DID VERSION: 1.0.0 
-- DID DESCRIPTION:   
-- DID BUSINESSRULES: EVERY DAY  
-- DID LASTMODIFIEDBY:  
******************* DOCUMENTATION TEMPLATE ********************  
*/ 


DECLARE @TASK NVARCHAR(50) = 'ST_SYNC_UHM_QUALIFIANTS_CONTACTS'; 


BEGIN 
SET NOCOUNT ON; 
BEGIN TRY 
 


-- ------------------ INIT -------------------- --  
-- TASK DETAILS TABLE 
IF OBJECT_ID('DBO.CFG_SYNCDETAILS', 'U') IS NULL 
BEGIN; 
  
    CREATE TABLE DBO.CFG_SYNCDETAILS ( 
        TASK NVARCHAR(50) NOT NULL CONSTRAINT PK_CFG_SYNCDETAILS PRIMARY KEY 
        , LAST_RUNDT DATETIME NULL 
        , LAST_SUCCESSDT DATETIME NULL 
        , LAST_RUNTIME NVARCHAR(8) NULL 
        , ERRORMSG NVARCHAR(MAX) NULL 
        , AQID BIGINT 
        , FGID BIGINT 
        , MTAID BIGINT 
    ); 
END; 
-- ------------------ LOGS -------------------- --  
IF EXISTS (SELECT 1 FROM DBO.CFG_SYNCDETAILS WHERE TASK = @TASK) 
BEGIN; 
    UPDATE DBO.CFG_SYNCDETAILS SET LAST_RUNDT = GETDATE(), ERRORMSG = NULL WHERE TASK = @TASK; 
END; 
ELSE 
BEGIN; 
    INSERT INTO DBO.CFG_SYNCDETAILS (TASK, LAST_RUNDT) VALUES (@TASK, GETDATE()); 
END; 
  
  
-- ------------------ PK & IDX ------------------ -- 
  
  
IF NOT EXISTS (SELECT 1 FROM SYS.INDEXES AS I WITH(NOLOCK) WHERE I.NAME = 'IDX_MATCH_NUM_CONTACT' AND OBJECT_ID = OBJECT_ID('DBO.SYNC_UHM_QUALIFIANTS_CONTACTS', 'U')) 
BEGIN; 
    CREATE NONCLUSTERED INDEX IDX_MATCH ON [DBO].[SYNC_UHM_QUALIFIANTS_CONTACTS] ([NUM_CONTACT]) /* INCLUDE ([X]) */ ; 
END;  

  
-- ------------------ REJECT MNGT ------------------ -- 
  
  
-- EMPTY BUSINESS KEY 
UPDATE TGT SET 
      TGT.OPTI_REJECTED        = 2 
    , TGT.OPTI_REJECTED_REASON = 'NUM_CONTACT' 
FROM DBO.SYNC_UHM_QUALIFIANTS_CONTACTS AS TGT 
WHERE (TGT.OPTI_REJECTED = 0  
OR TGT.OPTI_REJECTED IS NULL) 
AND (TGT.NUM_CONTACT IS NULL  
OR TGT.NUM_CONTACT = '')
;  
  
  
-- ------------------ MATCH TABLE ------------------ -- 
  
  
-- CHECK 
IF OBJECT_ID('DBO.TMP_MATCH_QLF', 'U') IS NOT NULL 
BEGIN; 
    DROP TABLE DBO.TMP_MATCH_QLF; 
END; 
  
-- CREATE 
CREATE TABLE DBO.TMP_MATCH_QLF( 
      SYNCID INT NOT NULL 
    , USERID INT NOT NULL 
    , PRIMARY KEY (SYNCID, USERID) 
); 
  
-- FILL ID_MAGELLAN
INSERT INTO DBO.TMP_MATCH_QLF 
    ( SYNCID 
    , USERID ) 
SELECT     
      SRC.ID AS SYNCID 
    , USR.ID AS USERID 
FROM DBO.SYNC_UHM_QUALIFIANTS_CONTACTS AS SRC WITH(NOLOCK)  
    INNER JOIN DBO.USERS_UHM_CONTACTS AS USR WITH(NOLOCK)  
        ON SRC.NUM_CONTACT = USR.NUM_CONTACT
WHERE (SRC.OPTI_REJECTED = 0  
OR SRC.OPTI_REJECTED IS NULL)
; 

  
-- ------------------ UPSERT ------------------ -- 
  
-- UPDATE FIRST... 
UPDATE TGT SET 
		  TGT.VALEUR_QUALIFIANT_CONTACT     = SRC.VALEUR_QUALIFIANT_CONTACT
        , TGT.DATE_CREATION                 = SRC.DATE_CREATION
        , TGT.OPERATEUR_CREATION            = SRC.OPERATEUR_CREATION
        , TGT.DATE_DERNIERE_MODIFICATION    = SRC.DATE_DERNIERE_MODIFICATION
        , TGT.OPERATEUR_MODIFICATION        = SRC.OPERATEUR_MODIFICATION
        , TGT.DATE_DEBUT                    = SRC.DATE_DEBUT
        , TGT.DATE_FIN                      = SRC.DATE_FIN
        , TGT.MODIFIED_DT                   = GETDATE()
FROM DBO.DATA_UHM_QUALIFIANTS_CONTACTS AS TGT 
    INNER JOIN DBO.TMP_MATCH_QLF AS MAT WITH(NOLOCK) 
        ON MAT.USERID = TGT.USERID 
    INNER JOIN DBO.SYNC_UHM_QUALIFIANTS_CONTACTS AS SRC WITH(NOLOCK) 
        ON  SRC.NUM_CONTACT         = TGT.NUM_CONTACT
        AND TGT.CODE_QUALIFIANT     = SRC.CODE_QUALIFIANT

                

  
-- ... THEN INSERT 
INSERT INTO DBO.DATA_UHM_QUALIFIANTS_CONTACTS  
    ( NUM_CONTACT
    , CODE_QUALIFIANT
    , VALEUR_QUALIFIANT_CONTACT
    , DATE_CREATION
    , OPERATEUR_CREATION
    , DATE_DERNIERE_MODIFICATION
    , OPERATEUR_MODIFICATION
    , DATE_DEBUT
    , DATE_FIN
    , USERID
    ) 
SELECT
      SRC.NUM_CONTACT     
    , SRC.CODE_QUALIFIANT
    , SRC.VALEUR_QUALIFIANT_CONTACT
    , SRC.DATE_CREATION
    , SRC.OPERATEUR_CREATION
    , SRC.DATE_DERNIERE_MODIFICATION
    , SRC.OPERATEUR_MODIFICATION
    , SRC.DATE_DEBUT
    , SRC.DATE_FIN
    , MAT.USERID
FROM DBO.SYNC_UHM_QUALIFIANTS_CONTACTS AS SRC WITH(NOLOCK) 
    INNER JOIN DBO.TMP_MATCH_QLF AS MAT WITH(NOLOCK)
        ON MAT.SYNCID = SRC.ID
WHERE  (SRC.OPTI_REJECTED = 0
OR SRC.OPTI_REJECTED IS NULL) 
AND NOT EXISTS ( 
    SELECT 1  
    FROM DBO.DATA_UHM_QUALIFIANTS_CONTACTS AS TGT WITH(NOLOCK)  
    WHERE TGT.USERID = MAT.USERID
    AND TGT.CODE_QUALIFIANT = SRC.CODE_QUALIFIANT

); 

-- ------------------ CLEAN UP -------------------- -- 
  
  
IF OBJECT_ID('DBO.TMP_MATCH_QLF', 'U') IS NOT NULL 
BEGIN; 
    DROP TABLE DBO.TMP_MATCH_QLF; 
END; 
  
  
-- ------------------ END LOGS ------------------ -- 
  
  
UPDATE DBO.CFG_SYNCDETAILS SET 
    LAST_RUNTIME = CONVERT(NVARCHAR(8), GETDATE() - LAST_RUNDT, 114) 
    , LAST_SUCCESSDT = GETDATE() 
WHERE TASK = @TASK; 
  
  
END TRY 
  
  
-- ------------------ CATCH IF ERROR -------------------- -- 
  
  
BEGIN CATCH 
    -- DECLARATION - ERROR PART 
    DECLARE @ERRORMESSAGE NVARCHAR(4000);   
    DECLARE @ERRORSEVERITY INT;   
    DECLARE @ERRORSTATE INT; 
    -- INIT - ERROR PART 
    SET @ERRORMESSAGE = CONVERT(NVARCHAR(50), ERROR_LINE()) + ' : ' + ERROR_MESSAGE();   
    SET @ERRORSEVERITY = ERROR_SEVERITY();   
    SET @ERRORSTATE = ERROR_STATE();   
    -- UPDATING LOGS W/ ERROR MESSAGE 
    UPDATE DBO.CFG_SYNCDETAILS SET  
        ERRORMSG = CONVERT(NVARCHAR(20), GETDATE()) + ' - LIGNE ' + @ERRORMESSAGE 
        , LAST_RUNTIME = CONVERT(NVARCHAR(8), GETDATE() - LAST_RUNDT, 114) 
    WHERE TASK = @TASK; 
    -- RAISING ERROR 
    RAISERROR(@ERRORMESSAGE        -- MESSAGE TEXT.   
            , @ERRORSEVERITY    -- SEVERITY.   
            , @ERRORSTATE        -- STATE.   
    ); 
END CATCH 
  
  
END
